label start:

    scene apartmentN with dissolve
    stop music
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 06 Fear of being mistaken.mp3" fadein 1.0
    "The drawer shakes slightly as you tug on it, but ultimately doesn’t give. It’s locked."
    "It’s still locked."
    "It’s cheap, an easy-build model from a discount store. It came with the apartment and you just never got rid of it. It wouldn’t take much force to just snap the lock."
    "Of course, you won’t do that. Like all locks, you just need the key. Breaking it would be admitting you don’t have the key."
    "It would mean admitting—"
    show apartmentNL with dissolve
    "Your phone lights up, the notification sound bouncing around the empty apartment." 
    "An email, the notification from your professional account. It’s later than you’d usually get one, from work at least. You keep meaning to tell HR you need a work phone but like everything else, you’ve been putting it off."
    "You shuffle your way over, the light hurting your eyes after sitting in the dark."
    "Maybe that’s why it takes you a moment to recognize the username of the sender." 
    # Phone Graphic: "1 Unread from: cygnusnapped@truemail.com: One Last Kiss for My Lover"
    show phone
    show notification
    with dissolve
    "You take a moment to breathe. Your hand shakes as you open the email. What strikes you first is that the formatting is surprisingly formal for them."
    window hide
    #Phone Graphic: "I don’t mean to haunt you, and I certainly don’t mean to hurt you. I love you more than anything, and I want you to know how I feel, just in case something stopped me from saying it before. It’s unfair of me to ask, but I know it’s not always fair between us. I’m trusting you with this, and thanking you in advance. Here is my final will and testament."
    show screen phone("I don’t mean to haunt you, and I certainly don’t mean to hurt you. I love you more than anything, and I want you to know how I feel, just in case something stopped me from saying it before. It’s unfair of me to ask, but I know it’s not always fair between us. I’m trusting you with this, and thanking you in advance. Here is my final will and testament.") 
    with dissolve
    pause
    hide screen phone
    hide phone
    hide notification
    with Dissolve (2.0)
    # BLACK SCREEN WITH BIG WHITE FONT IN THE MIDDLE THAT SAYS "3 YEARS AGO"
    stop music fadeout 1.0
    scene black with Dissolve(2.0)
    centered "{cps=5}3 Years ago..."
    scene cityAtNight with Dissolve(2.0)
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 05 Sweet night and friends.mp3" fadein 1.0
    show cygrinkiss1 with dissolve
    "They sit next to you on the railing, the last one out of the theatre. Even this early in autumn, the chill starts to set in at night."
    "The heat, the warmth, practically radiates off of them, unaffected by the cold. You find yourself inching closer to them." 
    C "Thanks again for coming, it means a lot to me. Hope I didn’t keep you waiting too long?" 
    "A little closer."
    E "No I’ve just been out here for a few minutes. Thank you for getting me tickets, you’re always an inspiration to watch!"
    "Did they just scoot closer?"
    C "Erin..." 
    "The cold suddenly seems much more biting, and their warmth turns to flame."
    C "I don’t want our relationship to change at all if you don’t want it to. I love how we are now!" 
    "Their words spill out quickly. Their hand tightens on the railing"
    C "But I have feelings for you. Romantically, I mean."
    C "And, I’m fine shelving those because like I said, I love being your friend and telling you I love you and not having to worry about putting any undue pressure on you!"
    C "And I’m not like, wasting away or saving myself or anything, but I’ve been thinking about how we talked about how comfortable and open we are…"
    C "So I just… wanted to get it out there." 
    "You stare at their hand, not meeting their eyes. You’re suddenly very aware how cold your fingers feel. And how sweaty your palms are." 
    E "I kinda figured. You’ve been really getting into my feelings lately"
    E "I feel the same. I guess I just didn’t want to say, because part of me is still scared."
    E "But you make me feel good! And I keep finding myself thinking of you. You fill me with comfort, and I love and cherish you for that." 
    "It would be too conspicuous to wipe your hands off now, you idiot." 
    E "Part of me was hoping it was an infatuation phase I could just, I don’t know, push through? But part of me wants to take advantage of this while it’s here. Like you said, I love how we are now. But I want to find out what we would look like together." 
    "You can’t look at them, your hand tightens around the rail. They don’t say anything, patient as always." 
    E "I’m sorry that I held back. I know you want me to be open. I do love you, I’m just unsure how to handle it."
    E "I want it, but I want you to be with someone better who can more sufficiently support you."
    "You’re a mess."
    E "I’m a mess." 
    E "I feel bad because I feel like I led you on, even though I do genuinely want you." 
    "The words come so much easier than you imagined they would. Cygnus broke the dam and now they can just spill forth." 
    E "I’m sorry." 
    "You remember to breathe out. You stop crushing your hand into the railing."
    hide cygrinkiss1
    show cygrinkiss2
    with dissolve
    "Warmth blooms from your hand as they lace their fingers in yours. Slowly lifting up, your palms meet, and your eyes follow to theirs. If they care about your clammy hands, they don’t show it on their face."
    "Somehow you both know to lean forward. You’re not sure who moves first, though you think it was probably them."
    show cygrinkiss3
    hide cygrinkiss2
    with dissolve
    "Your first kiss as something more than just friends isn’t long, but their warmth lasts on your lips even after you pull away." 
    "They don’t let go of your hand, even though you’re pretty sure you’re sweating even more now." 
    hide cygrinkiss3
    show cygrinkiss2
    with dissolve
    # CG: Silhouettes holding hands
    C "You don’t need to apologize, you’ve done nothing wrong. I love you too. I’m just relieved you feel the same way!"
    C "I mean, I thought you did, but…" 
    E "I like to be coy I guess? I mean I figured as well I just didn’t say because uhhhh…"
    "You know it’s because it’s far, far too frightening. It’s too much weight, responsibility, chances for things to go wrong. If you never take a chance you can never get hurt." 
    "But here they are, taking that chance for you."
    C "It’s the sort of thing that’s not dangerous until you point it out." 
    "As if your body finally catches up to your feelings, a shiver runs down your arm."
    "The bird finally lets your hand go."
    stop audio fadeout 1.0
    hide cygrinkiss2
    show cygrinkiss1
    with dissolve
    C "Ah, it’s pretty late, and kinda cold. We should head home, and we can talk about this more wh—" 
    "You grab the sleeve of their jacket."
    play music "audio/music/Komiku_-_02_-_Le_Grand_Village.mp3" fadein 1.0
    E "Would you… like to come home with me?"
    show black with Dissolve(4.0)
    scene cgerinsex with Dissolve(2.0)
    "You don’t have a lot of experience to compare it to, but this is the best sex you’ve ever had." 
    "Cygnus is hungry for you. It’s good, you feel that same desire, in a way you haven’t before." 
    C "Erin, will you… is it okay to ask you to say my name again?"
    "You wrap your arms around them, pulling them closer to you. Your hearts beat hard against each other."
    E "Cygnus…"
    E "I want your name in my mouth, and I want to struggle to say it between my breaths." 
    "So this is love."
    stop music fadeout 1.0
    scene black with Dissolve(2.0)
    centered "{cps=5}2 Years ago..."
    # BLACK SCREEN WITH BIG WHITE FONT THAT SAYS "2 YEARS AGO"
    scene mailroom
    play music "audio/music/Komiku - It's time for adventure vol 3 - 06 A good bass for gambling.mp3" fadein 1.0
    show e neutral arms_shy shirt_shy pants_shy at rightFlip,easein(2.0,-1.0,0.3)
    show boss at left with easeinleft
    B "So that’s everything! If your keycard isn’t activated by the end of the day go see Martin down in Security! Anything else you need!?"
    "The owl looks down at you, unblinking. He leans just far enough forward to be verging on uncomfortable." 
    E@annoyed"Uh, no, thank you."
    show boss at jump
    B "Great! This is a tough job, and it takes lots of dedication, but I think you’ll find it very rewarding!" 
    B "Our cases are pretty straight forward, mostly dementia and Alzheimer's victims, sorting out those with clouded last days, straightforward stuff! Still! If you find your cases weighing on you, pop right on up to our counselor offices on floor 3 and I’m sure they’ll fix you up in a jiffy!" 
    "The owl hoots a little." 
    B "That’s all I’ve got for your Erin! See you at the welcome party!"
    hide boss
    with easeoutright
    show e neutral arms_crossed shirt_crossed pants at ijsberen
    "He leaves, and you turn to your work. The Department of Last Thoughts and Unspoken Words. Subdepartment for Hospice and Natural End of Life." 
    "It’s simple work, at least for you. Within someone’s own heart in their final days, you find the directions in their inner worlds and give it back to the people who live on after them." 
    "Processed, archived, and passed on for wherever next it goes in the bureaucratic chain. Your coworkers speak about the emotional cost, but it doesn’t seem to apply to you."
    "It’s formulaic, almost unreal, and in time it becomes routine for you. There’s always more. You do the work, and get paid."
    scene bar with Dissolve(2.0)
    show e neutral arms_crossed shirt_crossed pants at rightFlip with dissolve
    "Every Friday your department goes out to party, or drink, or hang out. You think mandatory attendance is implied, but you don’t ask."
    show faggylizzard at easein(0.5,0.3,0.3),infiniteJump(0.2) with dissolve
    show boss at easein(0.5,0.0,0.0),infiniteJump(0.15) with dissolve
    show c arm_resting happy jacket_left tail_clothes leotard pants at easein(1.0,-1.0,0.67)
    "You invite Cygnus, and they join you a couple of times, but bars have never really been their scene."
    show c laugh jacket_laugh jacket_left arm_laugh leotard_pitflash at flip with dissolve
    hide faggylizzard
    hide boss
    hide c
    with easeoutleft
    show e pensive
    "They laugh off having to shepherd drunk people (even though no one drinks nearly that much), but they seem uncomfortable. You stop inviting them."
    show e happy at left,unflip with ease
    show boss at center,flip,infiniteJump(0.75)
    show faggylizzard at right,flip,infiniteJump(0.5)
    with easeinright
    "It’s alright, though. You enjoy yourself with your coworkers, even if there remains a layer of separation between your life and theirs."
    show e laugh with dissolve
    "It’s an easy place to float, apart from each other, but still within earshot. You laugh at their jokes, and listen about their lives. You orbit the outside of the dynamic, but there’s still space for you."
    scene black with Dissolve(2.0)
    "It makes up for the long hours, and how you’re too tired to do much of anything after work."
    stop music fadeout 1.0
    scene cygnusRoom
    play music "audio/music/Komiku - A Tale is never forgotten - 01 The main reason we are here.mp3"
    show e pensive at ijsberen
    with dissolve
    "Everytime you use Cygnus’ spare key to get into their apartment you curse yourself for not getting them a key to yours yet. Next time." 
    "You do the same thing you do every time this happens. First, the kettle on the stove. There aren’t any clean cups so you move all the dishes to one sink and wash two in the other."
    "You know they probably haven’t eaten yet either, so you wash a butter knife and a plate as well. The rest can wait for now." 
    "Peppermint tea, their favorite, which isn’t saying much since it’s the only kind of tea they like. A sandwich, pb&j, from their seemingly endless supply of the two staple ingredients." 
    "You set it in their place on their tiny dining room table. You’re not entirely sure where any of their roommates are, but as usual they’re missing from any sort of task that might require responsibility."
    scene black with dissolve
    scene cygnusRoom
    show e neutral at leftFlip
    show blankiedthefuckup at easein(0.1,-1.0,-1.0)
    with dissolve
    "You knock gently, before entering their bedroom."
    show e neutral at right
    show blankiedthefuckup at Transform(xalign=0.0,yalign=10.0)
    with ease
    "Stepping past old ballet programs, folded laundry still in a basket, and the occasional knitted stuffed animal, you sit on the edge of their bed."
    show e neutral at easein(2.0,1.0,0.4)
    "You gently shake the tangle of blankets hiding your partner. Their usual heat clings to them. The beak and few feathers that emerge glisten with sweat."
    "Usually when it’s like this they’ve been in bed for almost a full 24 hours." 
    "You keep your voice gentle, and low. They don’t like being seen like this." 
    E@speaking "Hey, how’re you feeling?"
    show blankiedthefuckup at spriteShake
    C "Hmm? Erin?"
    "They croak, their voice raspy." 
    show e arms_heart shirt_heart with dissolve
    E@speaking"Hey. Have you eaten today? I made you some dinner." 
    "You gently run your hand along their form. The blanket is a little damp." 
    E@speaking"Why don’t you get up and come take your meds, okay?" 
    "They seem to wake up a little more, too much inflection and care in their words, as their voice strains." 
    show blankiedthefuckup at spriteShake
    C "Yeah, yeah okay. Thank you, lover. Lemme just get up." 
    "You sit with them, as minutes pass."
    show e pensive arms_shy shirt_shy with dissolve
    E "Cygnus."
    show blankiedthefuckup at Transform(yalign=1.0)
    with ease
    "They rise this time, still clinging to a blanket."
    show blankiedthefuckup at spriteShake
    C "Oof, right. Thank you, I’m moving now." 
    show e neutral arms_heart shirt_heart at easein(1.5,0.4,0.67)
    show blankiedthefuckup at easein(2.5,0.0,0.34)
    "You spend the rest of the evening taking care of them, helping where you can with chores, making sure they take their meds, which they at least keep up on even when they’re in an episode like this."
    hide blankiedthefuckup with easeoutleft
    show c_sad pants tail at easein(2.5,-1.0,0.34)
    "It’s hard to see them this way. You wish you could do more, spend more time with them. Wish that they could live with someone who cared more."
    show e pensive at spriteShake
    show c_sad at spriteShake
    "The two of you let out a sigh in almost perfect synchronization."
    E@speaking "um…"
    hide c_sad
    show c happy arm_resting tail_noclothes pants at pos3 with dissolve
    C "...heh"
    show c laugh arm_hand with hpunch
    show e happy with dissolve
    C "Pffft, hahaha!"
    "Their laugh rings out, a little more light shining into the room with you."
    show c happy with dissolve
    C "I adore you so, Erin." 
    stop music fadeout 1.0
    scene black with Dissolve(2.0)
    centered "{cps=5}2 Years ago..."
    # BLACK SCREEN WITH BIG WHITE FONT THAT SAYS ONE YEAR AGO
    scene car with Dissolve(2.0)
    # BG: Car
    "The rain falls hard against the windows of the cab. It’s late enough that all the buses have stopped running, and you had to call a rideshare to get home."
    play music "audio/music/Komiku - It's time for adventure vol 2 - 04 Time.mp3" fadein 1.0
    "You check your phone, something which is something you have been putting off doing all day for reasons you can’t name, but have a slow dread about." 
    "You have only 3 messages. A good sign. All three are from Cygnus. The first is around 9 in the morning."
    # Phone Graphic: "Happy anniversary! I love you so much, I love being your friend, and your partner. I can’t imagine my life without you in it (\^v^/)"
    # "Shit."
    show phone
    show screen phone("{size=*0.8}Happy anniversary! I love you so much, I love being your friend, and your partner. I can’t imagine my life without you in it (\^v^/){/size}\n{size=*0.5}Received 9:03AM{/size}")
    with easeinbottom
    "The next reads from 5pm."
    hide screen phone
    show screen phone("{size=*0.8}Happy anniversary! I love you so much, I love being your friend, and your partner. I can’t imagine my life without you in it (\^v^/){/size}\n{size=*0.5}Received 9:03AM{/size}\n\n{size=*0.8}Final night of the show, can’t wait to see you there! It’s just the Nutcracker, admittedly not my most exciting show, but I think you’ll enjoy the costumes. Your comp tickets are at the register. I put you on the side closest to me ;>{/size}\n{size=*0.5}Received 4:58PM{/size}")
    with dissolve
    # Phone Graphic: "Final night of the show, can’t wait to see you there! It’s just the Nutcracker, admittedly not my most exciting show, but I think you’ll enjoy the costumes. Your comp tickets are at the register. I put you on the side closest to me ;>" 
    "You quickly scroll to the final one, an hour ago, a little after midnight."
    hide screen phone
    show screen phone("{size=*0.8}Happy anniversary! I love you so much, I love being your friend, and your partner. I can’t imagine my life without you in it (\^v^/){/size}\n{size=*0.5}Received 9:03AM{/size}\n\n{size=*0.8}Final night of the show, can’t wait to see you there! It’s just the Nutcracker, admittedly not my most exciting show, but I think you’ll enjoy the costumes. Your comp tickets are at the register. I put you on the side closest to me ;>{/size}\n{size=*0.5}Received 4:58PM{/size}\n\n{size=*0.8}Text me that you’re home safe. Goodnight.{/size}\n{size=*0.5}Received 12:20AM")
    with dissolve
    # Phone Graphic: "Text me that you’re home safe. Goodnight." 
    "Shiiiiiit."
    hide phone
    hide screen phone
    with easeoutbottom
    "Another performance completely missed. You don’t actually remember the last one you went to. Cygnus won’t get mad at you, they never do, but you know it hurts them."
    "Of course now is when your mind decides to remind you of all the conversations you’ve had with them about being forgotten."
    "How they dread not being remembered, and feel like they only take up space in someone’s mind when they’re directly in front of them." 
    "Anniversaries mean a lot to them, because it reminds them the time they spent is real."
    "And finally how even though this ballet isn’t that impressive, since you’ve missed the last few, it would mean the world to them if you could catch at least one of the performances." 
    "You realize you haven’t been to a single one since you’ve gotten this job."
    scene ErinApartment_Night with Dissolve(1.5)
    "When you finally stumble into your apartment, it feels like more of a tomb than a home."
    stop music fadeout 1.0
    scene greenroom with Dissolve(2.0)
    # BG: Green Room
    play music "audio/music/Komiku - It's time for adventure vol 4 - 04 I got 99 broadswords but this one isn't one (store's theme).mp3" fadein 1.0
    show c laugh arm_laugh jacket_laugh leotard_pitflash jacket_left tail_clothes pants at pos1
    show e happy arms_shy shirt_shy at pos3
    show k arms_crossed shirt_crossed pants_crossed at pos8,flip
    with dissolve
    C "And this is Kosta, sometimes we audition for the same part, but mostly we end up on stage together, since he’s one of the few guys here that has the core strength necessary to lift me."
    show c happy arm_neutral jacket_neutral
    show k laugh
    with dissolve
    K "Hey, nice to meet you! I’ve heard a lot about you from Cygnus" 
    show k neutral
    "You can only begin to wonder how many times Cygnus must have told him that you’re unreliable and downright cold."
    K@speaking"I’m really glad that Cy has someone like you looking out for them."
    "You’re almost blinded by the raw aura of affection this man has for the world around him."
    "You somehow feel if the building were to collapse right now, he would find a way to shield both you, and Cygnus, from the debris. And somehow he'd come out no worse for wear."
    "He also has a vague accent, though you wouldn’t be able to place where from."
    show e arms_crossed shirt_crossed with dissolve
    E@speaking"Oh, thank you. It’s nice to meet you too."
    show c laugh arm_laugh jacket_laugh with dissolve
    C "Kosta is also the reigning Culpable Cog: Endeavor champ here at the ballet, so hopefully they can give you a run for your money where I am so sorely lacking."
    show c happy arm_neutral jacket_neutral
    show k neutral arms_resting shirt_normal pants_normal
    show e surprise arms_heart shirt_heart
    with dissolve
    E "Oh! Who do you main?"
    K@laugh "Anthrax. Can’t resist the gender neutral." 
    E@speaking"Damn… a counter…" 
    K@speaking"Oh?"
    E@laugh"We’ll have to play sometime. I main June!"
    show e happy
    show c laugh arm_laugh jacket_laugh
    show k concern at pos9 with ease
    "Kosta pulls back in mock horror" 
    K@speaking"Cygnus I didn’t know you were dating a feral beast!" 
    show k laugh with dissolve
    show e happy at pos4,flip with ease
    E@speaking "I didn’t know you were friends with a chaser."
    show e surprise
    show c surprise jacket_surprise arm_surprise
    show k speaking
    "Wait."
    show e pensive arms_crossed shirt_crossed with dissolve
    "That was definitely too far."
    show c happy arm_neutral jacket_neutral
    show e surprise at unflip
    with dissolve
    show k smug arms_crossed shirt_crossed pants_crossed at pos7 with ease
    K "Uh, yeah? Have you seen their thighs?"
    show e happy with dissolve
    "He turns and gestures at Cygnus."
    show k smug arms_crossed shirt_crossed pants_crossed at spriteShake
    K "Have you seen their thighs?"
    show e laugh
    show k laugh
    show c surprise arm_hand jacket_hand
    with hpunch
    show c happy arm_neutral jacket_neutral with Dissolve(2.0)
    "You both burst out laughing. Cygnus seems mildly confused, but is always ready to bask in the glow of a compliment." 
    scene bar with Dissolve(2.0)
    show c laugh arm_laugh jacket_laugh jacket_left tail_clothes leotard_pitflash pants at pos7,flip
    show k laugh arms_resting shirt_normal pants_normal at pos4
    show e laugh arms_heart shirt_heart at pos1
    with hpunch
    "The night continues, to the bar, and then back to his apartment for a first to 50."
    scene black with Dissolve(2.0)
    "The partial hangover the next day is worth it."
    stop music fadeout 1.0
    # BG: Erin’s Apartment
    scene erinBedroom
    play music "audio/music/Komiku - It's time for adventure vol 2 - 10 Something to save.mp3" fadein 1.0
    show c neutral arm_hand jacket_hand jacket_left leotard pants at pos7,flip
    show e neutral arms_heart shirt_heart at pos3
    with Dissolve(2.0)
    "Cygnus drapes over the only piece of furniture in your room like a tapestry."
    "Moments like these remind you how easy it was to fall in love with them."
    C@speaking"Maybe… if you let me move in with you, I’d finally get to meet some of your friends. I mean you’ve met my sibling, I want to meet your family!"
    show e pensive with dissolve
    "You both pause."
    C@speaking"Okay I don’t want to meet your family—"
    E@speaking"No—"
    show e happy
    C@speaking"Yeah no."
    show c happy
    C "But I want to meet the other important people in your life!"
    show e pensive
    E@speaking"Of course. It’s just hard for me I guess. I don’t know." 
    E@speaking"I’ve had bad experiences before, introduce someone and then break up shortly after." 
    show e pensive arms_shy shirt_shy with dissolve
    E "Same with moving in, it feels like a really scary step. What if we don’t work out?"
    C@speaking"I’m patient, I can wait, it’s just something I want you to think about, okay?"
    E "Okay."
    show e sad
    "For some reason the weight of letting people know about your partner bears down on you heavier than you want to admit." 
    scene cygnusRoom with Dissolve(2.0)
    show c_sad leotard tail pants at pos2,Transform(yalign=5.0)
    show e annoyed arms_crossed shirt_crossed at center,flip
    with dissolve
    C "Thanks for checking in on me, again. I’m sorry that I missed your messages."
    "You suppress the sigh." 
    E@speaking"It’s okay, I’m just a little frustrated. We don’t spend a lot of time together, so I wish that it wasn’t spent like this."
    show e neutral
    hide c_sad
    show c_angry leotard tail at pos2,standUp
    "They sit up"
    show e surprise with dissolve
    show e pensive at pos6 with ease
    C "You mean you don’t take much time away from work. You’re always exhausted!"
    "They’re right, which somehow makes it more frustrating."
    C "I’m sorry I slept through most of your day off, but you should have told me you were taking one. I didn’t expect you to cancel your plans on my behalf." 
    show e arms_shy shirt_shy
    E@speaking"I wanted to surprise you." 
    "You know it’s a terrible excuse, but they don’t call you on it." 
    C "And I appreciate that! But now we’ve both kind of wasted this time together."
    show e surprise with dissolve
    show e pensive with dissolve
    "The words leave their mouth like rocks sinking in water, you can feel their regret before they even finish the sentence."
    hide c_angry
    show c neutral arm_resting leotard tail_noclothes pants at pos2
    with dissolve
    C@speaking"Well, I don’t think time with you is ever wasted, but I know it’s not really what either of us would have preferred." 
    show e neutral arms_crossed shirt_crossed
    E@speaking"No, it’s not. I’m sorry, I should have communicated better. And I know that depressive episodes don’t necessarily run on a schedule.." 
    C@speaking"It’s okay, learning experience for both of us."
    hide c
    show c_sad leotard tail pants at pos2
    with dissolve
    C "I know I’m kinda gross and sweaty right now, but… will you hold me for a little while?"
    show e happy arms_heart shirt_heart with dissolve
    "You were hoping they’d ask." 
    show e at pos4 with ease
    E "Of course." 
    stop music fadeout 1.0
    scene black with Dissolve(2.0)
    centered "{cps=5}A few months ago..."
    # BLACK SCREEN WITH BIG FONT THAT READS A FEW MONTHS AGO
    # BG: Erin’s Apartment
    scene ErinApartment_Day
    show e neutral arms_crossed shirt_crossed at pos2,Transform(yalign=5.0)  
    show c neutral arm_neutral leotard_pitflash tail_clothes jacket_left jacket_neutral pants at pos6,flip,Transform(yalign=5.0)
    with dissolve
    "You sit across from each other. Cygnus absent mindedly stirs their tea. At this point you know they won’t finish it."
    play music "audio/music/Komiku - A Tale is never forgotten - 01 The main reason we are here.mp3" fadein 1.0
    show c speaking arm_speaking jacket_speaking
    C "I want, I need more from you. You’ve been so absent from my life lately. I understand affection is hard for you to show, and that time is one of the ways that you make up for it so…"
    show e pensive
    C "Where are you?"
    C "I get to see you maybe two hours a week total, if I’m lucky."
    C "I’m worried that you’re pulling away instead of telling me how you feel, and it’s frustrating, it makes me feel like you don’t trust me."
    show c neutral arm_neutral leotard_pitflash tail_clothes jacket_left jacket_neutral
    "They stop stirring their tea, looking up at you." 
    show c happy arm_neutral jacket_neutral with dissolve
    C "I will always be here for you, you can lean on me. I have the strength! I know things have been hard for me recently, but I can’t show you that I’m strong too unless you give me the chance, and trust me to understand you." 
    C "You’re not gonna cause me to spiral if you tell me something I don’t want to hear, especially since you don’t even know what my feelings will be." 
    show c neutral arm_neutral leotard_pitflash tail_clothes jacket_left jacket_neutral with dissolve
    "You think they’re waiting for you to say something, but you can’t find the words. Hesitantly, they start again."
    hide c
    show c_sad leotard tail_clothes jacket pants at pos6,flip,Transform(yalign=5.0)
    show e sad
    with dissolve
    C "I don’t know how you’re feeling, because you tell me you’re alright, or tired, even when that’s not how you act."
    C "I try to take you at face value, I don’t want to poke and prod, but beyond being your partner, also as your friend,"
    C "As someone who loves you, in many ways, I’m really worried Erin." 
    E@speaking"I appreciate the worry, but I feel as though you need to focus on yourself still." 
    E@speaking"It’s hard to see you suffering, and I don’t know what to do, when you’re miserable all the time." 
    "There’s a pain in your chest. It feels like someone is bearing down on you."
    E@pensive"And when I try to make time for you, you disappear on me."
    hide c_sad
    show c_angry leotard tail_clothes jacket at pos6,flip,Transform(yalign=5.0)
    with dissolve
    "You feel like you’re in trouble. You’re doing something wrong."
    show c_angry at spriteShake
    C "You only hear about the things that are hard because when I only have access to you for such a short amount of time, I want to get the things that matter the most out of the way first." 
    show e sad arms_shy shirt_shy at spriteShake
    C "It’s hard to share my life with you, Erin, when you don’t make space for me in yours."
    "You feel the tension in your legs, a buzzing in your arms." 
    C "I have you when I need you, but not when I want you. I feel like I’m in this limbo waiting for you to come home, but I don’t know if you want me there."
    show e surprise with dissolve
    show e sad with dissolve
    "That hurts more than you were expecting it to."
    "You don’t want to admit it, but it’s because you don’t know either." 
    show e pensive arms_crossed shirt_crossed with dissolve
    E@speaking"I think I need space, real space, Cygnus." 
    E@speaking"Distance, to pull myself together. And figure out what I want, and what I’m capable of giving." 
    "This time Cygnus waits for you patiently."
    show e arms_heart shirt_heart with dissolve
    E@speaking"I love you, but I miss when things were easier. Our relationship has become an obligation, and not one I look forward to."
    C "Erin… I understand, but relationships have obligations. Things aren’t always going to be easy, but that doesn’t make them bad. Yes, it’s more work that we both have to put in to maintain it long term."
    hide c_angry
    show c speaking arm_speaking jacket_speaking jacket_left leotard_pitflash tail_clothes pants at pos6,flip,Transform(yalign=5.0)
    with dissolve
    C "But I want that future with you." 
    show c neutral arm_neutral jacket_neutral jacket_left leotard_pitflash tail_clothes
    E@speaking"You’re right Cygnus."
    show c happy arm_neutral jacket_neutral jacket_left leotard_pitflash tail_clothes at flip,Transform(yalign=1.0) with ease
    "They stand up, looking at you hopefully."
    show e sad arms_crossed shirt_crossed with dissolve
    show c neutral arm_neutral jacket_neutral jacket_left leotard_pitflash tail_clothes
    E "But I don’t know if it’s something I can commit to as I am now."
    scene black with dissolve
    # BG: Black
    "When they leave for the night, you feel guilty, but relieved." 
    "You still text them, still talk about your day now and then, but you can feel the distance between you two."
    "It’s strange, being the one who initiates now. It used to be Cygnus, always so eager to talk to you, but lately it’s become hard to pry words out of them."
    stop music fadeout 1.0 
    # BLACK SCREEN THAT WITH BIG FONT THAT READS ONE WEEK AGO (and then in smaller shakier font) MAYBE TWO? MAYBE THREE?
    centered "{cps=5}One week ago...\n\n{size=*0.5}{cps=25}maybe two?{cps=1} {cps=25}maybe three?"
    "It feels unreal at first."
    "Like moving under water."
    "You feel like your body should be having more of an extreme reaction. You should collapse. You should be gripping your phone so hard it cracks, or your hands bleed, or shake." 
    "But you don’t. You calmly listen to the voice on the other end. You thank them."
    "You think that it must be hard to tell someone that a loved one has died." 
    "Maybe you feel a connection from your job. You tell the important words and memories to someone, and then that someone has to tell the person that they matter to."
    "You stare at the only locked drawer in your apartment, and try to think of the first time you ever saw Cygnus in person. For some reason you can’t remember." 
    "It feels too sad to bear, to think of the last time you saw them." 
    "You decide to go to bed instead."
    "You don’t get up the next morning, except to call into work."
    "Time goes by in a haze. One day you try to open the drawer, and it becomes a new part of your ritual."
    "Your leave for mourning is running out. You’re almost excited, to have an obligation to force you back into the world. You don’t remember much of the past few weeks, and you feel like you’ve been putting something off."
    # BLACK SCREEN WITH BIG WHITE TEXT THAT READS TONIGHT
    centered "{cps=5}Tonight."
    "You’re a little impressed, strangely, that Cygnus had set this up."
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 06 Fear of being mistaken.mp3" fadein 1.0
    "Some sort of deadman’s switch in code form, sent out if they don’t push a button to delay it." 
    "You’d be too worried about accidentally forgetting about it and then having to explain some uncomfortable things to the people in your life on the receiving end of it." 
    "The email is mostly formalities. First a list of passwords and posts to make, who to tell what."
    "A similar, but less revealing email has been sent to their sibling, who should help you with any legal issues of inheritance, and what to do with Cygnus’ savings, along with any sort of funeral arrangements." 
    "The scariest part though…" 
    show phone
    show screen phone("{size=*0.8}I want you to be the one to work my case. No one knows me like you do, and I know what you see will be meant for your eyes.\n\nI don’t want to leave anything between us after I’m gone. I want to be someone you can carry with you, proud of who I was, and who we were together. You know I always had trouble crying, so you’ll have to cry enough for both of us now.\n\nOne last kiss, one last goodbye, that I didn’t get to say. Please, Erin, play me out. If there’s something after this, I promise I’ll be shining down on you from wherever it is. I love you.")
    with easeinbottom
    # Phone Graphic: "I want you to be the one to work my case. No one knows me like you do, and I know what you see will be meant for your eyes. 
    # Phone Graphic: "I don’t want to leave anything between us after I’m gone. I want to be someone you can carry with you, proud of who I was, and who we were together. You know I always had trouble crying, so you’ll have to cry enough for both of us now. 
    # Phone Graphic: "One last kiss, one last goodbye, that I didn’t get to say. Please, Erin, play me out. If there’s something after this, I promise I’ll be shining down on you from wherever it is. I love you." 
    "You breathe in, and out."
    stop music fadeout 1.0
    hide phone
    hide screen phone
    with Dissolve(2.0)
    centered "{size=*2.5}{cps=74}Cygnus Leapt."
    # TITLE CARD
    scene mailroom
    show security at pos6,flip
    show e pensive arms_shy shirt_shy at pos1
    with dissolve
    # BG: Office
    "Despite how much time you’ve spent here the past two years, it feels strange being back again."
    "It seems silly now that you were so dedicated to always being here, you’re not sure why you put so much of your life into this place." 
    "Maybe it’s just the empty building, everyone having gone home hours ago."
    show security at jump
    G "Yeah feels like you were such a regular, ‘s been quite awhile since I seen ya!"
    play music "audio/music/Komiku - Tale on the Late - 03 The road we use to travel when we were kids.mp3" fadein 1.0
    show security at pos7,unflip with ease
    show e annoyed
    with dissolve
    "Well, almost everyone." 
    "The security guard has been rambling at you for a solid 20 minutes. You’re grateful someone was here to let you in, but now you wish he would leave you alone."
    G "I heard about the accident, sad stuff, sad stuff. You know some of the guys were actually betting on whether or not you’d come back." #kill yourself dude
    show e pensive    
    show security at spriteShake,flip
    with dissolve
    G "Oh uh! Not me though! Glad you’re feeling up to it."
    "You’ve talked with Kosta about working graveyard shifts before. You know enough of them make you kind of lonely."
    show security at unflip
    show e annoyed
    with dissolve
    G "I know after my first car’s alternator gave up the ghost, I was too bummed out to even consider any form of transport other than walking." #really, REALLY kill yourself
    "But you’re finding it very hard to be sympathetic right now. Politely excusing yourself from conversations has never been your strongest suit, but this guy seems immune to hints."
    G "You know they say you never really recover from your first love, but you never recover from your first car—"
    show security at pos6,flip with ease
    show e annoyed arms_crossed shirt_crossed at spriteShake
    "You decide to bite the bullet." 
    E "I wouldn’t be able to tell you, I don’t drive. Thank you for letting me in, but I have a lot of work to catch up on." 
    show security at jump
    G "Oh! Of course of course, say no more say no more. I’ll leave you to it, but if you need me I’m right down the hall, by the front."
    show security at pos8 with ease
    show e annoyed at ijsberen with ease
    "He waits for you to say something else, but you ignore him, organizing your work in front of you, repeatedly, until he finally leaves." 
    stop music fadeout 1.0
    show security at flip,easein(2.5,0.8,2.0)
    "You feel a little bad for being short with him, but you’re weary and tired, and you want to finish this journey while you still have the strength."
    play music "audio/music/Komiku - It's time for adventure vol 2 - 10 Something to save.mp3" fadein 1.0
    hide security
    show e neutral at center with ease
    "You set the Last Thoughts and Unspoken Words down in front of you. The form they take changes depending on whoever the current head of the department is."
    "Right now that’s a reel of film (apparently based on the saying of your life flashing before your eyes), and a manilla folder that never gets too messy or too full, but contains all of the words in whatever order you need them in." 
    "The Last Thoughts are the week before the end of life, with the occasional chronological jump into a memory for context."
    show e pensive at easein(2.5,0.5,0.4)
    "The Unspoken Words are things that were felt, or thought, or said, but not to the right person, or at the right time. Feelings choked back to spare others, or yourself, or maybe just never meant to be said outloud."
    "You reach your hand out to start, but pause, just centimeters before making contact."
    show e sad arms_heart shirt_heart at spriteShake
    "The weight of Cygnus’ absence suddenly feels unbearable. When was the last time you heard their voice? The last time you saw them laugh?"
    "When was the last time you felt their burning warmth?" 
    "You close that chasm before it can open up all the way, take a shaky breath, and gently rest your hands on the film reel." 
    # BG: Swirl [UNLOCK THE SUN UNSPOKEN WORDS!]
    show mailroom at VHS,swirl
    show e surprise arms_heart shirt_heart at VHS,AnimatedAberate
    with Dissolve(2.0)
    "Two years and you still haven’t gotten used to the feeling of leaping into someone else’s perspective."
    "Like jumping into the deep end of the pool, but the water is viscous, and it seeps into your very being."
    show e neutral arms_crossed shirt_crossed at VHS,AnimatedAberate with dissolve
    "A star ignites into being, a constellation follows. You sink further into the experiences and feelings of Cygnus, stopping at the edge."
    "Some of your coworkers prefer to fully take on their cases, but you find you work best as a passenger, a witness to their final journey."
    "Letting their experiences and feelings flow through you."
    scene car
    # BG: Car
    show c_sad leotard jacket tail_clothes jacket_left pants at pos1
    show e surprise at pos9,VHS,ghost(opacity=0.67),flip
    with vpunch
    "Sunday and you find yourself suppressing a yawn in the passenger seat of Cygnus’ car."
    "It’s nostalgic, the amount of times you’ve been driven home late at night by them, woken up to stumble inside before passing out the rest of the way, over the years of your friendship." 
    "But this time it’s their exhaustion you feel, punctuated by an unmistakable feeling of loneliness." 
    "They sigh, long and deep, before starting their car. You feel a spike of anxiety and pensiveness from them as they quickly fumble to turn the cd player off." 
    "The drive itself is quiet, a routine they’re comfortable with on their way home." 
    show layer master at WhiteNoise
    with dissolve
    scene cygnusRoomNight
    show e surprise at pos9,VHS,ghost(opacity=0.67),flip
    show c_sad sad jacket leotard tail_clothes pants at pos3
    with Dissolve(2.0)
    "Things shift like a dream, pulling you into their room."
    show c_sad at pos1,flip,Transform(yalign=5.0) with ease
    show c_sad -jacket with dissolve
    show c_sad -leotard -tail_clothes -pants tail with dissolve
    "Cygnus haphazardly strips down for bed, careful to be quiet, a consideration you know their roommates rarely extend them."
    show e pensive at VHS,AnimatedAberate
    "You feel the anxiety radiate off of them as they repeatedly write, delete, and rewrite a text… to you." 
    "Eventually they land on 'Finished up the first week of performances. Just checking that you’re coming to see the one on Sunday?' before falling face first onto their bed." 
    hide c_sad
    show blankiedthefuckup at pos1,flip,Transform(yalign=5.0)
    with dissolve
    "You’re still a little jealous of their ability to simply sleep, and not toss and turn and ruminate like you do."
    stop music fadeout 1.0
    show e pensive at pos7,VHS,ghost(opacity=0.67),flip with ease
    show layer master at WhiteNoise,StillAberate,VHS
    with dissolve
    scene cygnusRoom
    play music "audio/music/Komiku - It's time for adventure vol 2 - 11 Remember this shadow.mp3" fadein 1.0
    show blankiedthefuckup at pos1,flip,Transform(yalign=5.0)
    show e pensive at pos7,VHS,ghost(opacity=0.67),flip
    with Dissolve(1.75)
    # BG: Cygnus apartment Midday. 
    "Monday, and Cygnus sleeps through their alarms and wakes around noon." 
    show blankiedthefuckup at ijsveren
    "You watch them roll around in bed as they browse on their phone and haphazardly nap on and off."
    show e annoyed arms_heart shirt_heart at spriteShake,VHS,ghost(opacity=0.67),unflip
    "Occasionally the yelling and crashing of their roommates echo through the hall. Sometimes loud enough it makes you flinch, though you don’t feel anything but annoyance from Cygnus." 
    show e neutral at VHS,ghost(opacity=0.67),flip with dissolve
    "Finally you, the you that remains in the past, with Cygnus, respond to their text. An attempt at an inoffensive message, confirming that you’ll be there, before asking how they’re doing." 
    show e surprise at spriteShake,VHS,ghost(opacity=0.67),flip with dissolve
    show e pensive at VHS,ghost(opacity=0.67),flip
    "What catches you off guard is the scoff they give as they stare at the screen."
    hide blankiedthefuckup
    show c neutral arm_resting tail_noclothes at pos1,Transform(yalign=5.0)
    with dissolve
    show c neutral jacket_left arm_resting leotard tail_clothes pants at pos2,Transform(yalign=1.0) with ease
    "As they run through some morning stretches and pull on their clothes, waves of different emotions wash over you." 
    "Incredulousness, pain, affection, hope, despair."
    show e arms_crossed shirt_crossed
    show c at easein(2.0,0.2,2.0)
    show e sad at VHS,ghost(opacity=0.67),AnimatedAberate with Dissolve(2.0)
    "You find yourself holding onto your body to ground yourself against these tides, the room spins, and it isn’t until the door slams shut that you remember they only had one thing to say in response."
    "'I’m functioning.'"
    stop music fadeout 1.0
    show layer master at WhiteNoise,StillAberate,VHS
    with dissolve
    scene cygnusRoomNight
    show e pensive at Transform(xalign=-0.2,yalign=1.0),VHS,ghost(opacity=0.67)
    show c happy jacket_neutral arm_neutral tail_clothes jacket_left leotard_pitflash pants
    show k neutral arms_crossed shirt_crossed pants_crossed at right,flip
    play music "audio/music/Komiku - It's time for adventure vol 4 - 04 I got 99 broadswords but this one isn't one (store's theme).mp3" fadein 1.0
    # BG: Cygnus Apartment, Evening
    "Tuesday, and Kosta is over, while shockingly none of Cygnus’ roommates are home."
    show k at spriteShake
    "The coyote is absent mindedly crushing opponents in his fighting game of choice, while chatting with Cygnus." 
    show e neutral
    K@speaking"So… is Erin coming to this one?"
    show c speaking arm_speaking jacket_speaking
    C "They said they are."
    show c happy jacket_neutral arm_neutral tail_clothes jacket_left leotard_pitflash pants
    K@speaking"Right, but are they?" 
    show e surprise with dissolve
    show e pensive with dissolve
    "Ouch." 
    show c at jump
    C "Yes, heh, I think they really are. They bought their own ticket this time."
    K@laugh"Ha, glad to hear it. I wasn’t sure how to tell the rest of the troupe that Erin went to get cigarettes and milk."
    show e annoyed
    show c neutral jacket_neutral arm_neutral
    "You don’t have to tap into their feelings to tell Cygnus is rolling their eyes."
    "There’s a tense silence, besides the tapping of Kosta’s controller." 
    K@speaking"So… how’re things going with them? Any updates?" 
    show e neutral
    "Cygnus sighs, lifting their legs into Kosta’s lap, and tilting their head back." 
    stop music fadeout 1.0
    show c speaking arm_speaking jacket_speaking
    C "It’s hard. I miss them, I hear his voice in all his favorite songs. I miss all these little things, the check ins, the way his lips pull up his fangs when he laughs. The way he leans forward and smiles when he tells me he loves me." 
    C "But the hardest part is how, I don’t know, big my emotions feel."
    hide c
    show c_sad jacket leotard pants tail_clothes at easein(2.5,0.5,0.55)
    with dissolve
    K@speaking"I’m not sure I understand." 
    "It starts as a ripple around Cygnus. Exhaustion, followed by what almost feels like a mask being washed away." 
    play music "audio/music/Komiku_-_13_-_Bleu.mp3" fadein 1.0
    show c_sad at spriteShake
    C "I just feel like I’m always holding myself back. Always on this verge of being far too much. Too needy, too affectionate, too sad, and not just for others, but for myself as well."
    show k sad
    show c_sad sad at spriteShake
    show e sad
    "Waves pour out of Cygnus. Love and longing, exhaustion and fear. It’s hard for you to withstand."
    show e neutral at left,VHS,ghost(opacity=0.67) with ease
    "You steel yourself, and swallow your urge to run. They wanted you to see them, you can’t back down from this." 
    C "I’m tired. Always holding myself back is exhausting, and I feel it’s only a matter of time before everyone else is washed away by it too."
    show k sad arms_resting pants_normal shirt_normal tail_sad with dissolve
    "Kosta sets down his controller, gently rubbing Cygnus’ leg with one hand."
    show k at easein(2.5,1.0,0.95)
    K "Hey, dude, you’ve been through a lot with Erin. You’ve seen each other at your worst. Something like what you have is really rare, and I know Erin thinks that too."
    K "Right now you’re both struggling but…"
    "Kosta pauses." 
    show k neutral tail at jump
    K "I’m proud of you both, and I think you’ll get through it. I’m optimistic!"
    K@laugh"It’s rough, it sucks, but you got this pookie." 
    "The tide slowly recedes, soaking back into Cygnus as they breathe in, but the mask doesn’t return."
    hide c_sad
    show c laugh jacket_laugh arm_laugh tail_clothes jacket_left leotard_pitflash pants at jump,Transform(xalign=0.55,yalign=1.0)
    show e happy arms_heart shirt_heart at VHS,ghost(opacity=0.67),AnimatedAberate with dissolve
    C "Thanks Kosta. Don’t underestimate yourself either, I appreciate you listening so patiently to my bitching." 
    K@laugh"Hey you can always complain, it’s one of life’s greatest luxuries."
    "The three of you sit in silence, soaking in the warmth of Cygnus’ affection."
    stop music fadeout 1.0 
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.5)
    scene balletStage
    play music "audio/music/Komiku - It's time for adventure vol 3 - 06 A good bass for gambling.mp3"
    show e surprise arms_heart shirt_heart at left,VHS,ghost(opacity=0.67)
    "Wednesday, and it’s the review day before returning to the second week of the ballet."
    show e neutral
    show c neutral leotard arm_resting tail_noclothes pants at easein(1.5,-1.0,0.95)
    show k neutral pants_normal at easein(2.5,-1.0,0.55)
    pause 2.5
    show c at flip
    show k at jump
    D "Phaethon, make sure you hold on to the Chariot for the full duration of that final note. I know it’s hard but you’re built different. You got this."
    "Cygnus and Kosta run through opening stretches together, as the director approaches." 
    show faggylizzard at easein(2.0,-0.5,0.33)
    D "My favorite Chariot! I’ve really got no notes for you, just keep it up!"
    show k at flip,jump
    pause 0.33
    show k at unflip 
    "He turns from Kosta to Cygnus."
    show e happy
    D "Cycnus, great as always. I think you’ve really come into the role. I just have a question for you to keep in mind while you dance." 
    stop music fadeout 1.0
    show layer master at WhiteNoise,StillAberate,VHS
    with dissolve
    show layer master
    with Dissolve(1.5)
    "You feel time start to slip before your vision even starts to blur. You’re used to jumping through time to be shown a memory, letting the current take you is instinct by now." 
    "The same place, but a different time. No costumes yet, but still far into rehearsal, not long ago." 
    show e surprise
    show faggylizzard at jump
    D "Nononono No! Cygnus, what is this?"
    play music "audio/music/Komiku - It's time for adventure vol 2 - 04 Time.mp3"
    hide c
    show c_angry leotard pants tail at Transform(xalign=1.0,yalign=1.0)
    show k concern
    with dissolve
    show e pensive at easein(0.5,0.0,-0.05),VHS,ghost(opacity=0.67)
    "The frustration soaks through Cygnus. Today the universe hates them, and they’re ready to bite back. Their feathers arch in a way you don’t see often, but still almost makes you flinch." 
    C "Am I doing the steps wrong, Mr. Emilie?" 
    show faggylizzard at jump
    D "Uff da, you may as well be! Where is the emotion? You are not just a dancer, but an actor as well!"
    show faggylizzard at jump
    show e annoyed arms_crossed shirt_crossed
    D "You are lifeless on my stage, you dance like a doll. This is not Coppelia. You are King Cycnus! You mourn Phaethon—"
    show faggylizzard at jump,flip
    pause 0.5
    show faggylizzard at unflip
    "The director gestures to a sheep who makes an apologetic face, not wanting to be part of scolding Cygnus." 
    show faggylizzard at jump
    D "Who died in big fiery explosion! You must be more than that!"
    D "Yes Cycnus you dance your steps correctly, but you must put the feeling behind them. This is your biiiig solo. You dance on my stage now, and you will shine like the constellation you and this role are both named after." 
    show faggylizzard at pos7 with ease
    show c_angry at bend with dissolve
    "The tiny, middle-aged lizard has to reach up to pat Cygnus on the shoulder. You notice them lean forward slightly so he’s in reach." 
    D "You are good bird, good dancer. I know you will do this better than anyone else in the troupe could."
    "He winks." 
    D "Otherwise I wouldn’t have cast you. Now! Let’s try it again from the top."
    show faggylizzard at flip,easein(1.5,0.33,-1.0)
    pause 1.0
    hide c_angry
    show c_sad pants tail leotard at Transform(xalign=1.0,yalign=1.0),flip
    show k sad tail_sad pants_normal 
    with dissolve
    pause 0.1
    show e pensive at pos1,VHS,ghost(opacity=0.67) with ease
    with dissolve
    "Cygnus breathes out, and with it flows a cold feeling. Sorrow, and resignation. You can feel them trying not to catastrophize."
    show c_sad at spriteShake
    "Suddenly it clicks, and you realize that this is the same day you told Cygnus you needed the space in your relationship." 
    show e sad at flip,VHS,ghost(opacity=0.67) with dissolve
    show k at pos4 with ease
    show c_sad sad
    "You turn away as they start their solo again, their sadness filling the room."
    show layer master at WhiteNoise,StillAberate,VHS
    with dissolve
    show layer master
    show e neutral arms_heart shirt_heart at left,unflip,VHS,ghost(opacity=0.67)
    hide c_sad
    show c neutral leotard arm_resting tail_noclothes pants at Transform(xalign=1.0,yalign=1.0),flip
    show k neutral tail at center
    show faggylizzard at easein(2.0,-0.5,0.33)
    with Dissolve(1.5)
    "Reality ripples around you as you’re back in the review, a similar setting to the scene you just witnessed."
    show faggylizzard at jump
    D "Do you think King Cycnus was sad to love Phaethon, mm? Do you think he regretted it?"
    show c at spriteShake
    show e neutral arms_crossed shirt_crossed pants
    "Something stirs in Cygnus, though you can’t tell what."
    stop music fadeout 1.0
    "They breathe in, and out."
    show layer master at WhiteNoise,StillAberate,VHS
    hide faggylizzard
    with dissolve
    show layer master
    show k neutral shirt_crossed arms_crossed pants_crossed at flip,right
    show c at pos6,unflip
    show faggylizzard at pos2
    show boss at pos4
    with Dissolve(1.5)
    "Thursday and it’s the cast circle after the show. The main announcements have been finished, and now Kosta is getting ready to speak."
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 13 Friendship is a beautiful link.mp3" fadein 1.0
    "Cygnus is nervous, anticipating something."
    K@speaking"Some of you know that recently I got my MLS, which I’ve been slowly chipping away at." 
    K@speaking "Well, I recently got a job offer at the local library, and so even though I’ve only been with you guys for a few years…"
    K@laugh"I’m officially announcing my retirement!" 
    show c at easein(0.5,0.6,0.59)
    pause 0.5
    show c at spriteShake
    pause 1.0
    show c laugh leotard_pitflash arm_laugh pants with dissolve
    show faggylizzard at infiniteJump(0.2)
    show boss at infiniteJump(0.15)
    "You feel Cygnus’ feelings quiver in reaction, something within them pulling back… before it bursts forth, warmth and pride for Kosta radiating from them."
    show e pensive
    "The dancers and stage tech cheer and clap, collapsing the circle in on Kosta to congratulate him. Many of the members give him a few affectionate pets, as his tail wags rapidly back and forth." 
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.25)
    scene greenroom
    show k neutral shirt_crossed arms_crossed pants_crossed at right,flip
    show c neutral leotard arm_hand tail_noclothes pants at pos4
    show e neutral arms_shy shirt_shy at left,VHS,ghost(opacity=0.67)
    with dissolve
    "Cygnus and Kosta finish cleaning up for the night, putting away makeup and costumes, wiping down the counters."
    K@speaking"Okay Cygnus, I’ve spent enough time around Erin that he’s taught me how to pick up your vibes by osmosis." 
    "You have?"
    show c happy leotard arm_hand tail_noclothes pants at pos4
    C "Now there’s two people who can do that? This is getting out of hand." 
    "That same pride simmers in Cygnus, but there’s a little loneliness behind it, swirling with shame." 
    "Their voice has that quiver that they struggle with when they’re trying not to show too much emotion."
    hide c
    show c_sad leotard pants tail_clothes at pos4
    with Dissolve(1.25)
    C "I’m just gonna miss you a lot Kosta."
    show c_sad at spriteShake
    "Kosta waits patiently for them to meet his eyes."
    show k laugh at pos6 with ease
    K@laugh"Dude… you’re not getting rid of me that easily!" 
    "Time shifts again." 
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.0)
    scene balletStage
    show e neutral at Transform(xalign=-0.1,yalign=1.0),VHS,ghost(opacity=0.67)
    show c neutral leotard arm_resting tail_noclothes pants at pos3
    show k neutral shirt_normal pants_normal arms_resting at pos7,flip
    # BG: Ballet Stage
    "Something within you understands that you are now a few years in the past." 
    "You have enough intuition to know that this is probably the first significant meeting between these two." 
    K@speaking"Hey I don’t want to be weird, like, about gender, or anything, but I am in awe of your ability to dance en pointe."
    "His accent is just a little bit thicker than the Kosta you know."
    show k smug arms_crossed pants_crossed shirt_crossed with dissolve
    K"I didn’t think they let guys learn that."
    "Kosta makes a face somewhere between accidentally stepping in something, and biting his lip."
    K "Uh not that you’re a guy. Man." 
    show k neutral
    show e happy arms_heart shirt_heart
    show c smug with dissolve
    "You’ve seen this look on Cygnus' face before. They’ve been out much longer than you, this wouldn’t phase them, but they do enjoy watching cis people stumble over themselves." 
    K@speaking"It’s just, I mean, you’re taller than me. Well, I think actually all the ballerinos here. And the ballerinas. Obviously." 
    K@speaking"So I kinda. Assumed. Which is rude, I know, sorry. I just. Uh, don’t mean to, how do you call it…" 
    "The bird reels him in gently." 
    show c laugh arm_laugh at spriteShake
    C "Haha, no you’re fine. I understand, and have gotten much worse, trust me." 
    C "Yeah I started dancing before I came out. I’d say that I had to fight to get to learn that, but admittedly it was mostly my parents showing up to antagonize the teacher until she let me." 
    show c smug arm_smug
    "They roll their eyes and give a mock huff." 
    C "They told me learning ballet would make me shorter and yet here I am. Now I’m well over the head of all my troupe members and they won’t let me play the fragile maiden I long so desperately to be in my heart." 
    show k smug with dissolve
    "Kosta picks up quickly." 
    K "Ah yes, I was worried that I would lift you too emphatically and you would just flitter off into the night, like petals on the wind, or perhaps a very well made paper plane." 
    show c surprise arm_surprise
    pause 0.1
    show c laugh arm_laugh with dissolve
    "Cygnus lights up at the mention of the lift, you feel their awe, and a little bit of fluster, blooming out from them." 
    C "Actually I am a little blown away by how easily you’ve lifted me. The few other guys I’ve convinced to try in the past act like they’re dying." 
    show c happy arm_neutral with dissolve
    K "I mean this is my job? Lmao? I just do the choreography they pay me to do."
    K "I didn’t put in all this work just to let the rest of my troupe down." 
    C "Well, you’re making it look easy. I’m glad we’re together on this dance." 
    "You remember this dance, it was the one you started your relationship after watching the closing performance of." 
    "You were focused on Cygnus the entire time, but thinking back on it, Kosta was quite impressive, both with his air time and with how many of the cast members he did lifts with back to back."
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.25)
    scene bar
    show e happy at Transform(xalign=-0.1,yalign=1.0),VHS,ghost(opacity=0.67)
    show c laugh jacket_laugh jacket_left tail_clothes pants leotard_pitflash at pos4,Transform(yalign=5.0)
    show k laugh shirt_normal pants_normal arms_resting at right,flip
    show e laugh arms_heart shirt_heart as e2 at pos1,Transform(yalign=5.0)
    "Time shifts again and you recognize the shitty karaoke bar that you and Cygnus used to sneak into when you were underage." 
    stop music
    "You, Kosta, and Cygnus are the only patrons currently singing, going back to back to back, much to the dismay of the rest of the sleepy Tuesday night crowd."
    "You haven’t gone in forever, but you always had fun." 
    show k at jump
    K "—who like to fuck! That’s what’s up!" 
    show k laugh at pos7,Transform(yalign=5.0) with ease
    "Kosta finishes his song and walks with a self satisfied expression back to your table." 
    show k smug arms_crossed shirt_crossed pants_crossed with dissolve
    K "Well well well. Don’t think you can outdo me on this one." 
    show c smug arm_smug jacket_smug
    "Cygnus raises their hands."
    C "Anymore and my throat is gonna give out for the next week. I think you win this o—"
    show e happy arms_shy shirt_shy as e2 at rightFlip with ease
    show e surprise arms_shy shirt_shy at spriteShake,VHS,ghost(opacity=0.67)
    show c surprise arm_surprise jacket_surprise
    show k laugh at flip,Transform(yalign=5.0)
    "You watch yourself confidently pick up a song and grab the mic." 
    "Oh. Oh no. You look back at the table, and sure enough, an alarming number of empty glasses of various shapes and sizes rest on the table." 
    "Your own dread and embarrassment is matched only by Cygnus' excitement, curious as to what your pick is going to be."
    show karaoke1 with dissolve
    play music "audio/music/Loyalty Freak Music - GAME JAM VOL1 !!! - 11 Party time.mp3"
    "You force yourself to watch, for Cygnus." 
    E "Um, dois, três, quatro!" 
    "The slurping noises you make into the mic draw the attention of the entire bar, and the bartender."
    E "Ahn~" 
    show karaoke1
    show karaoke2
    with dissolve
    "The slurping continues, Kosta has paused mid sentence, his mouth open."
    E "Mmm, unf~" 
    "And Cygnus? You’re afraid to feel just how much secondhand embarrassment you must have been subjecting to them to."
    "And yet all you can feel is… pride? They’re so thrilled to see you up there, shaking your hips and making blowjob sounds whole heartedly." 
    "And, of course, this shouldn’t surprise you, underneath that pride is a very intense horny thirst. They want you, and while you’ve always known this, it’s almost staggering just how much they do." 
    "Even while you’re making a fool of yourself on stage." 
    stop music
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.25)
    scene bar
    show e happy at Transform(xalign=-0.1,yalign=1.0),VHS,ghost(opacity=0.67)
    show k laugh shirt_normal pants_normal arms_resting at pos7,flip,Transform(yalign=5.0)
    show e laugh arms_heart shirt_heart as e2 at pos1,Transform(yalign=5.0)
    "Later in the evening, you see yourself sitting next to Kosta. You vaguely remember Cygnus having to step away, but now they’re here, lurking in the back, watching you two interact." 
    K "Dude, I’m so glad you got to come out with us!"
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 13 Friendship is a beautiful link.mp3" fadein 1.0
    E "Oh, thank you, it’s been fun."
    K "Cygnus always talks about you, and unsurprisingly they’re right. You’re really easy to get along with."
    show e happy arms_heart shirt_heart
    "It still feels ridiculous to be told that. There’s no way." 
    K "You do a good job listening, and you’ve got such a good sense of humor." 
    show k neutral with dissolve
    show e happy as e2 with dissolve
    K@speaking"I mean there’s no way I would have ever sung that lol." 
    K@speaking"But also, you really light up the people around you. We should hang out more!" 
    E "Ah, yeah, I want to. It’s just hard for me to find time with work." 
    show e annoyed arms_crossed shirt_crossed at spriteShake
    "What a lame excuse. It’s true, but you can’t believe you didn’t set harder boundaries with your job." 
    "It just felt easier to focus on work, than to try and insert yourself into other people’s lives." 
    "Easier to keep yourself at a distance, where you can’t disappoint each other." 
    K@speaking"It’s no worries man. Just worth keeping in mind, I’m always down to hang!" 
    show k laugh with dissolve
    K "If you ever get bored, you know where to find me." 
    show c happy jacket_neutral arm_neutral jacket_left tail_clothes pants leotard_pitflash at easein(2.0,-1.0,0.4),Transform(yalign=5.0)
    "Cygnus makes their way back towards your booth. They’re filled with pride that the two of you get along so well. You can feel how much the both of you mean to them." 
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.5)
    scene cygnusRoomNight
    # BG: Cygnus’ bedroom
    "Time shifts again and you’re pretty sure you’re in Cygnus’ bedroom. You turn around and—"
    stop music
    show dismounted with hpunch
    "You shouldn’t be seeing this! Probably!"
    "You’ve seen plenty of pre, post, and during coital interactions, but not of your partner! And your friend!"
    "This isn’t the kind of thing you care very much about. You wouldn’t have minded if Cygnus slept with pretty much any of their friends, but they were always very careful to let you know, and give plenty of opportunities to tell them otherwise." 
    "Still, for the first time in your career you feel like you might be intruding a little." 
    "Cygnus dismounts Kosta, gently tugging the filled condom out from between their cheeks, before tossing it in a nearby bin." 
    # BG: Cygnus’ Bedroom
    hide dismounted with Dissolve(2.0)
    show c smug arm_smug tail_noclothes at flip,pos4,Transform(yalign=5.0)
    show k smug at left,Transform(yalign=5.0)
    with dissolve
    play music "audio/music/Komiku - It's time for adventure vol 2 - 10 Something to save.mp3" fadein 1.0
    "You feel what you would expect from afterglow, but also a lightness from Cygnus, a lessening of pressure, like surfacing from the deep." 
    K "Hey do you want me to finish you off?" 
    "Kosta licks his lips while making a jerking motion with his hand. Cygnus scoffs a little in return." 
    C "Dude look down, I already have." 
    "Sure enough Kosta’s front is coated in a layer of cum." 
    show k laugh
    K "Woah, so quiet, I didn’t even realize during my own finish. I was worried I was disappointing you." 
    show c laugh arm_laugh
    C "Heh, no. Don’t worry, Kosta, I just don’t tend to make much noise. I am however stealing the shower first."
    hide c laugh with easeoutleft
    show k concern
    "The bird starts to slip into their bathroom."
    K "Wait, what? Noooo please Cygnus you didn’t even get anything on or in you! At least give me a damp washcloth. It's gonna be so hard to get this out of my fur!"
    show k concern at flip with ease
    hide k concern with easeoutleft
    show whatthefuck at flip,easein(2.0,1.5,1.0)
    "...You’re going to be thinking about this for awhile." 
    scene cygnusRoomNight with Dissolve(2.0)
    show c happy arm_neutral tail_noclothes at left,Transform(yalign=5.0)
    show k neutral at flip,pos4 with easeinleft
    show e neutral arms_crossed shirt_crossed at rightFlip,VHS,ghost(opacity=0.67) with Dissolve(2.0)
    "When Kosta finally gets out of the shower, Cygnus sits on their bed, tapping away at their phone. You know they’re texting you." 
    show k smug
    K "Wow, already giving Erin the update? What are my rankings, what does he think?" 
    show c laugh arm_laugh
    C "Pfft, just because I keep him abreast of my sordid affairs, I don’t think that means he wants the full play by play." 
    show k smug at flip,spriteShake
    K "Are you sure? Can you invite him over?"
    show c neutral arm_hand with dissolve
    "Cygnus shoots him a look." 
    play music "audio/music/Komiku - It's time for adventure vol 2 - 04 Time.mp3"
    C "Actually…"
    "Ah, shame is already starting to cloud the waters. They’re embarrassed about this." 
    C "It’s been rough lately. I’m a hopeless romantic, and Erin is… so much more grounded." 
    show e pensive
    show c happy arm_resting with dissolve
    show k neutral with dissolve
    C "I love them so much, and we’ve been friends for such a long time, I don’t think there’s a future without us in each other’s lives." 
    C "But I don’t know what we’re going to look like soon."
    "Cygnus unfolds to Kosta, what you’ve done to them." 
    show c speaking
    "But also the ways they love you, the way you expand the world around them."
    show c happy arm_resting
    show k speaking
    "More than that, something that makes you ache, is the way they cherish sharing the important things in their life with you. It’s laced with an undercurrent of fear, that you’re pulling away."
    "That they’re doing something wrong. That they’re weighing you down, a cage you force yourself into."
    show k neutral
    show c speaking
    show e sad at spriteShake
    "They know what they want, but they don’t know how to ask you. They don’t know if they deserve it." 
    "You know they didn’t deserve this." 
    "Before you can truly begin to spiral, time pulls you back to Cygnus’ present." 
    stop music fadeout 1.0
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.5)
    scene greenroom
    show c_sad pants leotard jacket tail_clothes at pos8
    show k sad pants_normal shirt_normal arms_resting tail_sad at rightFlip
    show e neutral arms_heart shirt_heart at left,VHS,ghost(opacity=0.67)
    "Kosta is hugging Cygnus, while they both tear up." 
    K "Dude you’re making me cry now!" 
    hide c_sad
    show c laugh arm_resting jacket_left pants tail_clothes leotard at pos8
    show k laugh tail
    with dissolve
    "They laugh."
    C "I’m sorry! I love you Kosta, I’m really glad we’re friends." 
    K "I love you too."
    show c happy at pos6 with ease
    show k smug with dissolve
    show e happy with dissolve
    "They separate from each other, wiping their eyes." 
    K "Besides I’m going from one low paying career into another, because I guess I hate money." 
    K "So I’ve got to stick with you and Erin so someone can still buy my drinks."
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.75)
    scene cygnusRoom
    # BG: Cygnus’ Apartment
    show e neutral at left,VHS,ghost(opacity=0.67)
    show c surprise jacket_left tail_clothes pants leotard_pitflash arm_surprise jacket_surprise at pos4
    show e neutral arms_shy shirt_shy as e2 at pos8,flip
    with dissolve
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 06 Fear of being mistaken.mp3" fadein 1.0
    "Friday morning and Cygnus is surprised that you are the one messaging them first."
    show c neutral arm_neutral jacket_neutral with dissolve
    "This is the last time you ever see Cygnus, here in their apartment. It’s almost too painful to bear. Something impossible to work through."
    "The pain is a kind that is unimaginable, you want to just give up. Go home and waste away again. Find some sort of floating pool between giving up completely and living in a world without them." 
    "But not this time. You bite down. You continue."
    "As you sit across from Cygnus, your words tumble out."
    show e speaking arms_shy shirt_shy as e2
    E "I’m sorry. I know I’m letting you down, and that makes it even harder."
    show e pensive arms_shy shirt_shy as e2
    E "I’m in this torn place where I’m not sure what I want, and I’m so intimidated by your confidence." 
    show e happy arms_shy shirt_shy as e2
    E "You shine so bright Cygnus, and I’m so proud of you, and who you’ve become. I just don’t know if I’m the one who is right for you."
    show e sad arms_shy shirt_shy as e2
    E "You deserve better." 
    show c speaking arm_speaking jacket_speaking
    C "Please stop telling me what I deserve, or what’s best for me." 
    C "I’m patient, I want to be with you Erin. You mean so much to me. You’ve changed me so much as a person, and for the better." 
    C "I wouldn’t be where I’m at, hell, I might not even be alive, if I didn’t have your support." 
    C "You’re my closest friend, not just my lover."
    C "Yes, it hurts. I miss you. But I want you to be sure."
    show c neutral arm_neutral jacket_neutral with dissolve
    show e as e2 at spriteShake
    "You wonder when you realized that you hated yourself for this. You wonder why it was easier to keep going in this limbo, instead of picking one way or another."
    "You were too scared to let go, and too scared to hold tight." 
    "And Cygnus, in their strong arms, felt like they were breaking you." 
    show e speaking arms_shy shirt_shy as e2
    E "I almost regret saying yes in the first place. Maybe it was a mistake."
    show e sad arms_shy shirt_shy as e2
    show c speaking arm_speaking jacket_speaking with dissolve
    "You feel something shift in Cygnus. A beginning, the start of a river, a flood, in their heart." 
    show c neutral arm_neutral jacket_neutral with dissolve
    "They watch you, still waters not betraying the deeper current." 
    E "I just. Need more time. And I’m sorry." 
    show c happy with dissolve
    "They smile at you, exhausted, but still, despite everything you’ve done, filled with love." 
    C "I understand, thank you." 
    show c at pos6 with ease
    show e neutral as e2 with dissolve
    "They rest their hands on yours, and you wait in silence together, for just a little while." 
    "For not long enough." 
    show e as e2 at flip,easein(2.0,0.8,1.1)
    "Before you leave, they call out."
    show c speaking arm_speaking jacket_speaking with dissolve
    C "Erin?"
    show e at easein(1.5,0.0,1.1)
    pause 1.0
    hide e with dissolve
    "You step into yourself, in time with your reflection through Cygnus’ memories." 
    show e speaking as e2 at flip
    E "Yes?"
    show e neutral as e2
    "Your own feelings mix together with Cygnus’. Regret, longing, a blistering need, a melancholy sadness, overwhelming joy, everything you’ve known in them and everything they’ve known in you." 
    show c happy arm_neutral jacket_neutral with dissolve
    C "I love you." 
    "You didn’t hesitate."
    show e happy arms_heart shirt_heart as e2
    E "I love you too." 
    stop music fadeout 1.0
    "Time shifts."
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.75)
    scene apartmentD
    show c happy jacket_left tail_clothes pants leotard_pitflash arm_neutral jacket_neutral at left
    show e neutral arms_crossed shirt_crossed at pos7,flip
    with dissolve
    "You know why you’re here. Something you knew you would have to face."
    "Here you are, standing in front of one of the only pieces of furniture you own, Cygnus smugly explaining their plan." 
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 05 Sweet night and friends.mp3" fadein 1.0
    show c speaking arm_speaking jacket_speaking with dissolve
    C "So, we agreed to meet in the middle, right?"
    show c neutral arm_neutral jacket_neutral with dissolve
    E@speaking"Yes, but I’m not entirely sure how you meet in the middle when it comes to moving in?"
    show c speaking arm_speaking jacket_speaking with dissolve
    C "Well, I don’t want to pressure you, but you’ve got the space, and I would really like to stop living with three to four other people who make a ton of noise at all hours of the night"
    show c neutral arm_neutral jacket_neutral with dissolve
    show e pensive with dissolve
    E "Right…" 
    show c speaking arm_speaking jacket_speaking with dissolve
    C "So I am gently exposing you to the idea, by commandeering this drawer. I am moving into this drawer and locking it."
    C "And, when you are ready for me to move in with you, I will open it for you, and give you your housewarming gift." 
    show c neutral arm_neutral jacket_neutral with dissolve
    E@speaking"I’m pretty sure a housewarming gift is something you give to the person newly moved in?" 
    show c speaking arm_speaking jacket_speaking with dissolve
    C "Well it was going to be an anniversary gift but it got finished at a weird time and I had to plan something else." 
    show c neutral arm_neutral jacket_neutral with dissolve
    E@speaking"You really didn’t have to, I haven’t gotten you anything."
    show c happy with dissolve
    C "Sure, but I wanted to. I’m quite fond of you, you know." 
    show c at flip
    "They turn to you, their voice loud, as if subtly hinting something to someone in the next room." 
    show c speaking arm_speaking jacket_speaking with dissolve
    C "THOUGH I WOULDN’T COMPLAIN IF YOU WANTED TO RETURN THE FAVOR SOMETIME." 
    show c happy arm_neutral jacket_neutral with dissolve
    show c at unflip
    show e happy arms_heart shirt_heart with dissolve
    "You roll your eyes at them affectionately, trying not to let it bother you." 
    "You feel their frustration as well. And a mix of shame, for not just saying what they mean."
    "They hold out the key to the drawer." 
    C "That said, as we both know, the important part of boundaries and choices is the ability to say no." 
    C "So I’m leaving the key with you over here in this drawer—"
    scene drawer1 with dissolve
    "They walk over to the other dresser in your room, opening one of the top drawers. They set the key down. They’re nervous, but also self satisfied with this scheme." 
    scene drawer2 with dissolve
    C "And if you want that space back, or need in there for some reason, it’s right here. No harm, no foul." 
    scene drawer3 with dissolve
    "And just like that they shut the drawer. Where is the key?! You wish the memories would let you open the drawer yourself." 
    scene drawer5 with dissolve
    "Days after the news about Cygnus, you had tried to open the drawer. It was locked, and so you sought the key, where they had left it for you."
    scene drawer6 with dissolve
    "But it wasn’t there."
    "And now here you are, still waiting for them to open it for you instead."
    "Still waiting for them to come home." 
    stop music fadeout 1.0
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.75)
    scene cygnusRoom
    show e pensive arms_crossed shirt_crossed at pos9,VHS,ghost(opacity=0.67)
    show c neutral jacket_left tail_clothes pants leotard_pitflash arm_neutral jacket_neutral
    with dissolve
    "Back in their present, Cygnus watches you walk down the street from their window until they can no longer see you. They sigh, and with that breath out they begin to dance."
    show c at ijsberen with ease
    "Simple steps, slow, and out of time, as though trying to figure out the choreography for the very first time." 
    "They let their feelings move their body, building confidence. A lilting waltz danced between the two of you. They draw upon a revelation awakening within them and then—"
    show c surprise jacket_surprise arm_surprise at left with ease
    R "Yo Cyg we’re getting pizza, do you want any specific toppings?" 
    show c neutral jacket_left tail_clothes pants leotard_pitflash arm_neutral jacket_neutral
    "They stop, the conclusion slipping between their fingers. Another sigh, one of resignation."
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.75)
    scene greenroom
    show c neutral jacket_left tail_clothes pants leotard_pitflash arm_neutral jacket_neutral at left
    show e neutral arms_heart shirt_heart at flip,pos8,VHS,ghost(opacity=0.67)
    with dissolve
    "Cygnus texts you."
    show phone
    show screen phone("thanks again for stopping by Erin. love you! lemme know when you make it home\n{size=*0.5}Sent 01:33PM")
    with easeinbottom
    pause
    show screen phone("thanks again for stopping by Erin. love you! lemme know when you make it home\n{size=*0.5}Sent 03:58PM{/size}\n\ni’ll be perfomin soon so i won’t be around until after\n{size=*0.5}Sent 4:00PM") with easeinbottom
    pause
    hide phone
    hide screen phone
    with easeoutbottom
    # Phone Graphic: "thanks again for stopping by Erin. love you! lemme know when you make it home" 
    # Phone Graphic "i’ll be perfomin soon so i won’t be around until after"
    show c at center with ease
    show k neutral arms_crossed pants_crossed tail shirt_crossed at right
    show boss at left
    show e pensive at unflip
    with dissolve
    "They set their phone aside, and join the pre-show cast circle. Like usual, they stand between the dancer portraying Phaethon, and Kosta." 
    play music "audio/music/Komiku_-_02_-_Le_Grand_Village.mp3" fadein 1.0
    D "Alright everyone, it’s the middle of the second week. I know this one doesn’t have the excitement of the opening or closing weeks."
    D "It’s easy to slack off and not give it your all. Cut corners on expressions or entrances in the back. Put enough, but not everything into each performance." 
    "The lizard gestures as wildly as he can while keeping his arms crossed, right over left, in the circle." 
    D "Don’t do that! This is someone’s first show, and someone’s last show." 
    D "This will be the first performance of ballet someone ever sees, and you have the chance to make sure it stays with them forever. To inspire in them a love of the art." 
    D "And, this is someone’s last show. Their final performance, the last chance they get to experience the magic we bring them."
    D "You are talented, hard workers. You sweat and cried and in Kosta’s case bled for this show. Give them a beautiful goodbye, and show the full breath of your talent and skill." 
    D "Both they, and you, deserve the best that you can give. Now, go out there and make each other proud! Break!"
    stop music fadeout 1.0
    scene black with dissolve
    # BG: Black Screen
    play music "audio/music/The_Falling_Swan.mp3" fadein 1.0
    "Cygnus breathes in, and then out." 
    "The torrent swirls inside them. If they could just shift their arms in the right way." 
    "Their heart would open up, the floodgates would release, and everything would flow out. Wash away everything holding them back, holding them down, haunting them." 
    "They step onto the stage." 
    "The show is ending, and Cygnus steps into their final solo." 
    scene danceKerche1 with Dissolve(2.0)
    # CG: Kerche Dance 1
    "King Cycnus learns of Phaethon’s fate, his lover, struck down by divinity, a blazing chariot out of control. There was no alternative, lest the sky burn down on the earth." 
    "Slowly, slowly, the dance starts." 
    "Could his tears have soothed that flame, could he have saved his lover, if he had been able to better express what he meant to him?"
    "King Cycnus mourns his lover so beautifully, so compellingly, that when he leaps from the cliff to join him, the gods raise him up as a constellation." 
    "As the steps pick up, Cygnus floats across the stage. The turns and jumps and passes release something within them."
    "A pirouette, a leap, a slow turn again, their body sways, the shoulders relaxed, their face shifting." 
    "The fouetté, in place, while the music slows. Each turn a showing of Cygnus’ control over their body. Each turn a little bit closer to the truth."
    "Revelation upon them." 
    "The final barrier breaks, and all of Cygnus’ feelings are subsumed by this conclusion. King Cycnus couldn’t wash away the burning of Phaethon’s flames."
    "But Cygnus can still embrace the love they have."
    "To mourn? To regret? No. To love someone is wonderful." 
    "Did King Cycnus regret loving Phaethon? Hardly, he wouldn’t have traded it for anything else." 
    "Cygnus doesn’t regret loving you, and now they dance for your love."
    "No matter where you stand once this dance is over, Cygnus would never trade away what you had, what you have, for anything else." 
    "Through their dance now, they share with you how wonderful it is to know someone else in the ways they know you. The ways that you know them." 
    "To have someone see you, and not turn away."
    "Where the sun kisses the ocean, is where you wait for each other." 
    scene danceKerche2 with Dissolve(2.0)
    "For you, the very stars shine down." 
    "To be so vulnerable, so honest, with someone else, is terrifying. It takes an act of faith to plunge so deep into the arms of someone else."
    "And here, now, for you, Cygnus leapt." 
    scene greenroom
    show c happy tail_noclothes pants arm_neutral at center,flip
    show k neutral arms_crossed pants_crossed shirt_crossed tail at right
    show boss at left
    with dissolve
    play music "audio/music/The_Falling_Swan_boxver.mp3" fadein 1.0
    C "Oh, haha, thank you very much. You were amazing too." 
    show c at unflip
    hide boss with easeoutleft
    show e happy arms_heart shirt_heart at rightFlip,VHS,ghost(opacity=0.67) with easeinright
    "The ballerinas file out after complimenting Cygnus on their solo. The director waits back to talk to them after. You can feel the pride coming off Cygnus, and also the exhaustion."
    show c at left
    show k at pos4
    with ease
    show faggylizzard at pos7,flip with easeinright
    "The same sort of tiredness that comes from catharsis. Something held back for too long and now let loose. So long pulling against a leash that now after breaking it they need rest." 
    D "King Cycnus! I’m lost for words! And you do not need any, it seems."
    D "What you said out there, with that dance. I have never seen you in such control, and yet filled with such passion." 
    "The lizard makes a sharp inhaling noise. It almost makes you jump but Cygnus seems to be used to it." 
    D "Yes. Good. Do again tomorrow." 
    show faggylizzard at pos3 with ease
    "He reaches up to pat Cygnus on the shoulder, and they lean down so he can reach."
    hide faggylizzard with easeoutleft
    "Kosta follows up, giving Cygnus such an enthusiastic hug it actually lifts the over 6 foot bird off the ground." 
    show k laugh at flip with dissolve
    K "DUDE, WHAT WAS THAT?" 
    show k at infiniteJump(0.15)
    K "It’s a good thing you’re already the star soloist or the rest of the troupe would be pissed you’re upstaging them."
    "The coyote excitedly bounces around Cygnus." 
    K "Holy shit birdie, you knocked it out of the park. I don’t even know what to say." 
    K "I’m proud of you. Put that on for Erin tomorrow and they’ll fall straight into your arms."
    "They look down at their slightly damp clothes."
    K "Your sweaty, sweaty arms. Damn we’re gonna have to get more water proof makeup. Go home and shower." 
    show k smug with dissolve
    show c laugh arm_laugh with dissolve
    show e laugh with dissolve
    C "Pffft, it’s a hard dance! Let’s see you do it." 
    K "I’d die. No thank you." 
    C "Alright, well then I’ll head home so I can carry your dumb ass tomorrow too." 
    "Cygnus swells with confidence, and pride. Relief and affection swim through them." 
    K "I’ll see you tomorrow then." 
    C "Of course. Thank you again Kosta."
    show layer master at WhiteNoise,StillAberate,VHS
    with Dissolve(1.75)
    scene car
    show c happy tail_clothes pants arm_resting leotard jacket_left at left
    show e happy arms_heart shirt_heart at rightFlip,VHS,ghost(opacity=0.67)
    with dissolve
    "Cygnus sighs, sitting in the driver’s seat for a few minutes, cooling down. The rapids inside them slowly calm, and they start the car." 
    stop music fadeout 1.0
    "The cd player spins up, and they hesitate, their hand over the volume knob, before turning it up."
    "As they drive home, they sing along with the track. A small bittersweet trickle is replaced by nostalgia and joy. You understand why. This is the album the two of you bonded over after you started dating." 
    show c laugh arm_laugh jacket_laugh at infiniteJump(0.33)
    C "I will ride your waaaaaaake! And find these waaaaaves! They say we can’t but I know that’s not truuuuue!"
    "You’re not sure if those are the lyrics, but you still sing along with them anyway." 
    scene black
    jump repeat
    label repeat:
        "You breathe in, and then out."
        play music "audio/music/Loyalty Freak Music - BLEEPS AND BLOOPS - 04 Making my bleep ready for action -eyes- -muscle-.mp3" 
        menu:
            "This is it. Are you ready?"

            "No.":
                jump repeat

            "Yes.":
                jump moveon
        
label moveon:
    scene car
    show layer master at WhiteNoise,VHS,AnimatedAberate
    show e sad arms_heart shirt_heart at rightFlip,VHS,ghost(opacity=0.67)
    play music "audio/music/Komiku - The adventure goes on, vol.1 - 10 Those dark memories I'm trying to escape myself from.mp3" fadein 1.0
    # CG: This was going to be a cg but our poor artist is suffering. You can just slap a million filters on the car BG and deep fry it or something and call it good. The only sprite visible should be Erin during this.
    "Abstract shapes. Last Thoughts censors traumatic events for standard viewing. What matters isn’t the car accident itself, but the feelings Cygnus has."
    "Mercifully, it’s quick. But, for just a second, just a frame, you catch one standout thought."
    C "Dammit, Erin’s key." 
    scene black
    pause
    scene apartmentN
    show c happy tail_clothes pants arm_resting leotard jacket_left at left
    show e sad arms_crossed shirt_crossed at rightFlip,VHS,ghost(opacity=0.67)
    with Dissolve(2.0)
    "One last shift, and you’re back in your apartment." 
    "Cygnus puts the key in the drawer but scoots it too far back, falling in the back between the pieces of cheap furniture and the wall."
    "You can feel the eyeroll coming off Cygnus. Ruining their moment with something stupid. They’re confident they’ll remember to tell you about it… and then they forget." 
    show e happy with dissolve
    "It almost makes you chuckle, it’s very in character for them, ridiculous, but well meaning." 
    # BG: Swirl
    show layer master at WhiteNoise,VHS,AnimatedAberate
    with Dissolve(1.5)
    scene black
    pause
    scene mailroom
    show e sad arms_crossed shirt_crossed at center
    with Dissolve(1.5)
    stop music fadeout 1.5
    # BG: Archival Room
    "And that’s it. That’s the end of the tape. The end of Cygnus, their final performance, their swan song." 
    hide e with easeoutright
    "The way home is a blur. The security guard has the sense to not bother you on your way out." 
    scene apartmentN
    # BG: Erin’s Apartment
    show e neutral arms_shy shirt_shy at left
    with Dissolve(2.0)
    play music "audio/music/Komiku - It's time for adventure vol 2 - 10 Something to save.mp3" fadein 1.0
    "You pull the dresser out of the way, and sure enough, there it is, the key, slightly dusty, on the ground. Right where Cygnus had accidentally dropped it." 
    show e at pos4 with ease
    "Shakily, you put the key into the lock. The drawers aren’t particularly well made, but you can still feel the lock gently click open."
    "You slide the drawer out carefully, though it feels almost weightless."
    "Inisde, rests a small pendant, threaded around a small silver chain. Within a glass case, a single feather. One of Cygnus’ crest feathers you realize, something they always took rigorous care of." 
    show e pensive with dissolve
    "All you can hear is your heartbeat. You crumple onto your bed, clutching the pendant to your chest." 
    "Cygnus…"
    "Welcome home." 
    stop music fadeout 1.0
    scene funeral
    play music "audio/music/Komiku - It's time for adventure vol 4 - 06 Someday I'll like you but before let me rest in my solitude (Lonely character's theme).mp3" fadein 1.0
    show e sad pendant_heart arms_heart shirt_heart at pos1
    with dissolve
    "You’re unsurprised at the sheer number of people that have come out to remember Cygnus, but it still makes you proud." 
    "That so many people were touched by their light. You feel lucky that they were such a bright one in yours. Admittedly, a part of you you’re less proud of, feels a sense of superiority that Cygnus wanted you over everyone else here." 
    "The wind here is gentle, not enough to disturb any of the decorations, but enough that there’s a slight chill in the air."
    "Jacket weather, as Cygnus would call it, their favorite." 
    "They might have preferred rain as well, but luckily the sky is only mildly cloudy, and the ceremonies can proceed uninterrupted." 
    show k sad shirt_normal arms_resting pants_normal tail_sad at easein(2.0,1.5,0.45),flip
    show e surprise with dissolve
    "The ballet troupe is here, and Kosta catches your eye, making his way towards you."
    "You’re a little frightened, he had been Cygnus’ confidant, and you can only imagine the resentment he must feel towards you." 
    "But you stand your ground. You owe it, to them, and to yourself." 
    "The coyote’s momentum starts to worry you, but he stops himself."
    K "Can I get a hug?"
    E "O-oh, of course." 
    show e neutral
    show k sad at easein(2.0,0.45,0.30)
    "He’s firm, and only a little taller than you. It doesn’t surprise you that he’s the one who had been in charge of doing lifts in the troupe. When he pulls back, he’s smiling." 
    show e happy
    show k laugh arms_crossed shirt_crossed pants_crossed at easein(0.5,0.3,0.45)
    K "Erin, it’s really good to see you, man. I was worried about you."
    show e funeral with dissolve
    E "I was sure you’d be upset with me. Considering everything with Cygnus."
    show k concern
    pause 0.34
    show k funeral
    with dissolve
    "He looks surprised for a moment, but it doesn’t take him more than a second to put together that you must have some more context about the nature of their friendship." 
    K "No, of course not. We both cared for Cygnus. And I care about you too, dude. You’re my friend! I’ve missed you." 
    show e sad with dissolve
    E "Right, but. They told you about us, and what happened, what I did."
    "He pauses, trying to let his words come to him."
    show k concern
    pause 0.34
    show k neutral
    K@speaking"It’s scary sometimes, to be close to something so big like that. I know sometimes it gets overwhelming. We both know sometimes it even overwhelmed Cygnus."
    K@speaking"But you’re brave, Erin. Braver than you think. Just please, please remember that you’re not alone, either." 
    show e funeral
    show k funeral at easein(2.0,0.45,0.30)
    "Kosta hugs you again, before he’s pulled off by other mutual acquaintances. You work your way through the crowd as well, talking with the people who knew Cygnus, recalling old stories, and listening to new ones."
    show k neutral at easein(1.0,0.3,-0.8)
    "Eventually speeches happen. You’re not sure how, people just seem to know it’s time. Cygnus’ director speaks, followed by Kosta, and then their sibling." 
    hide k
    "Finally, it’s your turn. You breathe in, and out." 
    "Standing in front of everyone, you expect to feel dread, but strangely your shared purpose brings you relief. The words you had spent nights practicing now seem to come naturally." 
    show k funeral arms_resting shirt_normal pants_normal tail_sad at rightFlip
    show boss at pos8,flip
    show sibling at pos7,flip
    show faggylizzard at center,flip
    show e funeral arms_shy shirt_shy pendant at left
    with easeinright
    E "It’s strange, getting someone’s will. Being in charge making sure their wishes were carried out. Cygnus wanted to be cremated, and planted alongside a tree, which is why we’re here on this nature reserve."
    E "This red oak will remain undisturbed and safe. And we’re not too deep in, so hopefully you can all come and visit them. I’m sure they’ll like that." 
    E "I know Cygnus really wanted a celebration of life, and not a stuffy funeral."
    "You smile at the crowd as you look over them. You’ve already cried twice today, and you see tears on their family and the faces of their friends, and their troupe." 
    E "We’re doing our best, I think. It’s a strange place to be, I understand. They left specific instructions for me." 
    E "‘I understand these things are for the living, not the departed. I’m already asking so much of you. Do what feels right to you, whether that’s sharing all of our stories, or preserving those treasures between us. As long as they rest in your heart, I’ll be happy.’"
    E "Their kind of  poetic, truly. I really don’t like speaking at these sorts of things. It’s hard to pick the right words, to navigate this strange kind of paradox." 
    E "You’re supposed to do what would make the departed happy, because it would make those still with us to know that you had. But you can’t make someone who is gone happy, because they’re not here with us." 
    "The words tumble out of your mouth. You feel yourself drift between a kind of divine inspiration, and every single word sounding out of place and stiff." 
    E "Cygnus was my partner."
    "You let that statement rest deep within the seabed of your heart. It feels good to say." 
    E "I love them so, so much, and I think I always will. They’ve left a lasting mark on me, one that I’m proud to carry."
    E "So much of who I am, and who I’ve grown to be, I’ve done alongside them. Cygnus helped me to keep growing and becoming a better version of myself. I hope I did the same for them." 
    E "Cygnus was always so proud to share their love, our love, with the world around them. So proud we were together, that they shone like the sun." 
    E "It’s my turn now. I’ll carry on, and love enough for the both of us." 
    E "Cygnus named themself after a constellation. Did you know that we have no way to denote a star that we suspect has faded out, but whose light we can still see?"
    E "Scientifically it doesn’t matter, the stars shine on anyway."
    E "But to me, Cygnus wasn’t just a star, they were a strong and beautiful sun." 
    E "And in the sun rises, and sunsets. In the stained glass, the glittering dew, the powerful, warm and loving rays." 
    show e funeral arms_heart shirt_heart pendant_heart with dissolve
    "You clutch the pendant around your neck. You can almost feel Cygnus' heartbeat against yours."
    E "In those moments, I will know that Cygnus and their love is still here with me." 
    # End, return to the main menu, with the new version. 
    return
